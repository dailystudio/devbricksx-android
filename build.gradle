buildscript {
    apply from: "$rootDir/buildscripts/versions.gradle"

    ext.kotlin_version = depVersions.kotlin()
    ext.ksp_version = depVersions.ksp()
    ext.nav_version = "2.3.1"
    ext.devkit_version = "0.5.0"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }

}

allprojects {
    repositories {
        google()
        mavenCentral()

        maven { url 'https://jitpack.io' }
    }

    tasks.withType(Exec) {
        doFirst {
            println commandLine
        }
    }

    apply from: "$rootDir/buildscripts/versions.gradle"
}

ext {
    ROOM_VERSION = depVersions.room()

    // publish credentials
    if (file("private.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("private.properties")))

        BINTRAY_KEY = props.getProperty('BINTRAY_KEY')
        BINTRAY_USER = props.getProperty('BINTRAY_USER')

        NEXUS_USER = props.getProperty('NEXUS_USER')
        NEXUS_PASS = props.getProperty('NEXUS_PASS')

        GPG_KEY_ID = props.getProperty('GPG_KEY_ID')
        GPG_KEY_PASS = props.getProperty('GPG_KEY_PASS')
        GPG_KEY_FILE = props.getProperty('GPG_KEY_FILE')
    } else {
        BINTRAY_KEY = ""
        BINTRAY_USER = ""
    }

    // Unsplash.com API
    if (file("sample-gallery/apikey.unsplash").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("sample-gallery/apikey.unsplash")))

        UNSPLASH_API_KEY = props.getProperty('API_KEY')
    } else {
        UNSPLASH_API_KEY = ""
    }

    // Compile Settings
    COMPILE_SDK = 33
    TARGET_SDK = 33
    BUILD_TOOLS = '33.0.0'
    VERSION_NAME = versions.devBricksXVersion()
    VERSION_CODE = versions.devBricksXVersionCode().toInteger()

    // Publishing Parameters
    SITE_URL = 'https://github.com/dailystudio/devbricksx-android'
    SCM_URL = 'https://github.com/dailystudio/devbricksx-android.git'
    SCM_CONNECTION  = 'scm:git@github.com:dailystudio/devbricksx-android.git'
    SCM_DEV_CONNECTION = 'scm:git@github.com:dailystudio/devbricksx-android.git'
    GROUP_NAME = 'cn.dailystudio'
    DESCRIPTION = 'Provide utils classes commonly used in Android application development.'
    DEVELOPER_ID = 'dailystudio'
    DEVELOPER_NAME = 'dailystudio'
    DEVELOPER_EMAIL = 'dailystudio2020@gmail.com'
    LICENCE_NAME  = 'The Apache Software License, Version 2.0'
    LICENCE_URL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

    RELEASE_REPOSITORY_URL = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'

    versions.dumpVersions()
    depVersions.dumpVersions()
}
