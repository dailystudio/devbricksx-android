buildscript {
    apply from: "$rootDir/buildscripts/versions.gradle"

    ext.agp_version = depVersions.agp()
    ext.kotlin_version = depVersions.kotlin()
    ext.ksp_version = depVersions.ksp()
    ext.compose_version = depVersions.compose()
    ext.nav_version = depVersions.navigation()
    ext.coil_version = depVersions.coil()
    ext.maven_publish_version = depVersions.mavenPublish()
    ext.devkit_version = "${versions.devBricksXVersion()}-${depVersions.devKit()}"

    ext.junit_version = '4.13.2'
    ext.android_junit_ext_version = '1.2.1'
    ext.android_junit_espresso_version = '3.6.1'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "com.vanniktech.maven.publish:com.vanniktech.maven.publish.gradle.plugin:$maven_publish_version"
    }

}

allprojects {
    repositories {
        google()
        mavenCentral()

        maven { url 'https://jitpack.io' }
    }

    tasks.withType(Exec) {
        doFirst {
            println commandLine
        }
    }

    apply from: "$rootDir/buildscripts/versions.gradle"
}

subprojects {
    tasks.matching { task ->
        // Disables the Javadoc generation task for every module
        task.name.toLowerCase().contains("javadocreleasegeneration")
    }.configureEach {
        enabled = false
    }
}

ext {
    ROOM_VERSION = depVersions.room()

    // publish credentials
    if (file("private.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("private.properties")))

        NEXUS_USER = props.getProperty('NEXUS_USER')
        NEXUS_PASS = props.getProperty('NEXUS_PASS')

        GPG_KEY_ID = props.getProperty('GPG_KEY_ID')
        GPG_KEY_PASS = props.getProperty('GPG_KEY_PASS')
        GPG_KEY_FILE = props.getProperty('GPG_KEY_FILE')
    } else {
        NEXUS_USER = System.getenv('NEXUS_USER')
        NEXUS_PASS = System.getenv('NEXUS_PASS')

        GPG_KEY_ID = System.getenv('GPG_KEY_ID')
        GPG_KEY_PASS = System.getenv('GPG_KEY_PASS')
        GPG_KEY_FILE = System.getenv('GPG_KEY_FILE')
    }

    // Unsplash.com API
    if (file("sample-gallery-core/apikey.unsplash").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("sample-gallery-core/apikey.unsplash")))

        UNSPLASH_API_KEY = props.getProperty('API_KEY')
    } else {
        UNSPLASH_API_KEY = ""
    }

    // Compile Settings
    COMPILE_SDK = 35
    TARGET_SDK = 35
    MINI_SDK = 21
    BUILD_TOOLS = '35.0.0'
    VERSION_NAME = versions.devBricksXVersion()
    VERSION_CODE = versions.devBricksXVersionCode().toInteger()

    // Java Compiler
    JAVA_VERSION = JavaVersion.toVersion(depVersions.java().toInteger())
    JVM_TARGET = depVersions.java()
    JVM_TARGET_INT = depVersions.java().toInteger()

    // Publishing Parameters
    SITE_URL = 'https://github.com/dailystudio/devbricksx-android'
    SCM_URL = 'https://github.com/dailystudio/devbricksx-android.git'
    SCM_CONNECTION  = 'scm:git@github.com:dailystudio/devbricksx-android.git'
    SCM_DEV_CONNECTION = 'scm:git@github.com:dailystudio/devbricksx-android.git'
    GROUP_NAME = 'cn.dailystudio'
    DESCRIPTION = 'Provide utils classes commonly used in Android application development.'
    DEVELOPER_ID = 'dailystudio'
    DEVELOPER_NAME = 'dailystudio'
    DEVELOPER_EMAIL = 'dailystudio2020@gmail.com'
    LICENCE_NAME  = 'The Apache Software License, Version 2.0'
    LICENCE_URL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

    RELEASE_REPOSITORY_URL = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'

    versions.dumpVersions()
    depVersions.dumpVersions()
}

apply from: './buildscripts/timepertask.gradle'