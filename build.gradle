buildscript {
    ext.kotlin_version = '1.3.71'
    ext.bintray_release_version = '0.9.2'
    ext.nav_version = "2.3.0-alpha06"

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "com.novoda:bintray-release:$bintray_release_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.withType(Exec) {
        doFirst {
            println commandLine
        }
    }
}

ext {
    // Jcenter publish credentials
    if (file("private.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("private.properties")))
        BINTRAY_KEY = props.getProperty('BINTRAY_KEY')
        BINTRAY_USER = props.getProperty('BINTRAY_USER')
    } else {
        BINTRAY_KEY = ""
        BINTRAY_USER = ""
    }

    // Compile Settings
    COMPILE_SDK = 29
    BUILD_TOOLS = '29.0.3'
    VERSION_NAME = version()
    VERSION_CODE = versionCode().toInteger()

    // Publishing Parameters
    SITE_URL = 'https://github.com/dailystudio/devbricksx'
    GIT_URL = 'https://github.com/dailystudio/devbricksx.git'
    GROUP_NAME = 'com.dailystudio'
    DESCRIPTION = 'Provide utils classes commonly used in Android application development.'
    DEVELOPER_ID = 'dailystudio'
    DEVELOPER_NAME = 'dailystudio'
    DEVELOPER_EMAIL = 'dailystudio2020@gmail.com'
    LICENCE_NAME  = 'The Apache Software License, Version 2.0'
    LICENCE_URL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
}

def String version() {
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    return versionProps['major'] + "." + versionProps['minor'] + "." + versionProps['patch']
}

def String versionCode() {
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    return versionProps['code']
}
